apply plugin: 'maven'
apply plugin: 'signing'

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        // https://github.com/novoda/bintray-release/issues/71
        excludes = ['**/*.kt'] // < ---- Exclude all kotlin files from javadoc file.
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        options.encoding = "utf-8"
        options.charSet = "utf-8"
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

// 强制 Java/JavaDoc 等的编码为 UTF-8
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives javadocJar
    archives sourcesJar
}

//Properties properties = new Properties()
//properties.load(project.rootProject.file('local.properties').newDataInputStream())
//
//def ossrhUsername = properties.getProperty("ossrhUsername")
//def ossrhPassword = properties.getProperty("ossrhPassword")

def PUBLISH_VERSION = android.defaultConfig.versionName //这个是直接获取的库gradle里配置好的版本号，不用到处修改版本号，只需要维护一份就可以。

//签名
signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.groupId = "io.github.charlotteumr.jv"
            pom.artifactId = "jsonview"
            if (isRelease.toBoolean()) {
                pom.version = PUBLISH_VERSION
            } else {
                pom.version = "${PUBLISH_VERSION}-SNAPSHOT"
            }

            pom.project {
                packaging 'aar' //我这里发布的是安卓的包，所有写的aar

                name "jsonview" // '发布库的简单名称'
                // optionally artifactId can be defined here
                description "a view for displaying json" // '发布包的描述'
                url "https://github.com/CharlotteUMR/JsonView" // '可以写公司官网地址或github个人页面地址'

                scm {
                    connection "scm:git@github.com:CharlotteUMR/JsonView.git" // 'scm:替换成项目git地址'
                    developerConnection "scm:git@github.com:CharlotteUMR/JsonView.git" // 'scm:替换为git开头的项目地址'
                    url "https://github.com/CharlotteUMR/JsonView" // '项目首页，可以是github项目的主页'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'CharlotteUMR' // '这里填写申请账号时候的全名就可以'
                        name 'Jerry' // '这里随意填写就可以'
                        email 'charlotteUMR@gmail.com'// '最好是申请账号时用的邮箱'
                    }
                }
            }
        }
    }
}
